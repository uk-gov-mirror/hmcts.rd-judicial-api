buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.0.83")
    }
}

plugins {
    id 'application'
    id 'jacoco'
    id 'pmd'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id "info.solidsoft.pitest" version '1.5.1'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.sonarqube' version '2.8'
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id "org.flywaydb.flyway" version "6.5.1"
    id "io.freefair.lombok" version "4.1.6"
    id 'uk.gov.hmcts.java' version '0.12.0'
}

apply plugin: 'java'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'project-report'
apply plugin: 'idea'

def versions = [
        lombok             : '1.18.6',
        reformLogging      : '5.1.0',
        reformS2sClient    : '3.1.2',
        serenity           : '2.0.76',
        sonarPitest        : '0.5',
        springBoot         : '2.3.4.RELEASE',
        springHystrix      : '2.1.1.RELEASE',
        springfoxSwagger   : '2.9.2'
]

mainClassName = 'uk.gov.hmcts.reform.judicialapi.JudicialApplication'
def jarName = 'rd-judicial-api.jar'

group = 'uk.gov.hmcts.reform.juddata'
version = '0.0.1'

sourceCompatibility = 11
targetCompatibility = 11

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

sourceSets {

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }

    functionalTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

    smokeTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.resources.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.resources.srcDirs
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    functionalTestCompile.extendsFrom testCompile
    functionalTestRuntime.extendsFrom testRuntime
}

pmd {
    toolVersion = '6.12.0'
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest, sourceSets.functionalTest, sourceSets.smokeTest]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSets = [
            'category/java/errorprone.xml',
            'category/java/bestpractices.xml'
    ]
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {

    systemProperty "scenario", System.getProperty('scenario')

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    finalizedBy aggregate
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
    baselineOnMigrate = true
    if (project.hasProperty("dburl")) {
        url = "jdbc:postgresql://${dburl}"
    }
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    executionData(test)
    reports {
        xml.enabled = true
        csv.enabled = false
        xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

pitest {
    targetClasses = ['uk.gov.hmcts.reform.judicialapi.*']
    excludedClasses = [
            'uk.gov.hmcts.reform.judicialapi.configuration.*',
            'uk.gov.hmcts.reform.judicialapi.JudicialApplication'
    ]
    threads = 10
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
    outputFormats = ['XML', 'HTML']
    timestampedReports = true
    mutationThreshold = 90
    useClasspathFile = true
}


project.tasks['pitest'].group = "Verification"

sonarqube {
    properties {
        property "sonar.projectName", "RD :: Judicial Reference api"
        property "sonar.projectKey", "RDJUDICIALAPI"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
        property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/judicialapi/configuration/**," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/JudicialApplication.java,"+
                "src/main/java/uk/gov/hmcts/reform/judicialapi/domain/*," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/service/impl/JudicialRoleTypeServiceImpl.java"
    }
}

project.tasks['sonarqube'].dependsOn test, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

repositories {

    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }

    jcenter()
}

dependencies {

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.10'

    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
    integrationTestCompile group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'

    compile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: versions.springBoot
    compile (group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc')
    {
        exclude group: "org.apache.tomcat.embed", module: "tomcat-embed-websocket"
        exclude group: "org.apache.tomcat.embed", module: "tomcat-embed-core"
    }
    compile (group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot)
    {
            exclude group: "org.apache.tomcat.embed", module: "tomcat-embed-websocket"
            exclude group: "org.apache.tomcat.embed", module: "tomcat-embed-core"
    }

    compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '9.0.37'
    compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.37'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: versions.springHystrix

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-cache', version: versions.springBoot

    compile group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '2.5.6'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.10.2'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.10.2'

    compile group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '2.3.3'

    compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

    compile group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformLogging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformLogging
    compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version: '0.1.0'
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: versions.reformS2sClient

    compile group: 'org.flywaydb', name: 'flyway-core', version: '6.5.1'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.14'

    compile group: 'com.google.guava', name: 'guava', version: '30.0-jre'
    implementation('org.apache.tomcat.embed:tomcat-embed-core:9.0.40') {
        because 'Apache Tomcat 10.0.0-M1 to 10.0.0-M9, 9.0.0-M1 to 9.0.39 and 8.5.0 to 8.5.59 could re-use an HTTP ' +
                'request header value from the previous stream received on an HTTP/2 connection for the request associated ' +
                'with the subsequent stream. While this would most likely lead to an error and the closure of the HTTP/2 connection, ' +
                'it is possible that information could leak between requests'
    }
    implementation('org.apache.tomcat.embed:tomcat-embed-websocket:9.0.40') {
        because 'Apache Tomcat 10.0.0-M1 to 10.0.0-M9, 9.0.0-M1 to 9.0.39 and 8.5.0 to 8.5.59 could re-use an HTTP ' +
                'request header value from the previous stream received on an HTTP/2 connection for the request associated ' +
                'with the subsequent stream. While this would most likely lead to an error and the closure of the HTTP/2 connection, ' +
                'it is possible that information could leak between requests'
    }
    implementation('org.apache.httpcomponents:httpclient:4.5.13') {
        because 'Apache HttpClient versions prior to version 4.5.13 can misinterpret malformed authority component in ' +
                'request URIs passed to the library as java.net.URI object and pick the wrong target host for request execution.'
    }

    testCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.3.0'

    testCompile("org.hamcrest:hamcrest-junit:2.0.0.0") {
        exclude group: "org.hamcrest", module: "hamcrest-core"
        exclude group: "org.hamcrest", module: "hamcrest-library"
    }

    testCompile group: 'com.h2database', name: 'h2'
    testCompile "com.github.tomakehurst:wiremock:2.19.0"
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
    testCompile group: 'org.mockito', name: 'mockito-inline', version: '3.1.0'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.4'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot

    testCompile 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'

    testCompile (group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity)
    {
        exclude group: 'com.vladsch.flexmark', module: 'flexmark-all'
        exclude group: 'javax.websocket', module: 'javax.websocket-api'
    }
    testCompile (group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity)
    {
        exclude group: 'com.vladsch.flexmark', module: 'flexmark-all'
        exclude group: 'javax.websocket', module: 'javax.websocket-api'
    }
    testCompile (group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity)
    {
        exclude group: 'com.vladsch.flexmark', module: 'flexmark-all'
        exclude group: 'javax.websocket', module: 'javax.websocket-api'
    }
    testCompile (group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity)
    {
        exclude group: 'com.vladsch.flexmark', module: 'flexmark-all'
        exclude group: 'javax.websocket', module: 'javax.websocket-api'
    }



    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot


    testCompile(group: 'org.yaml', name: 'snakeyaml', version: '1.25') {
        force = true
    }

    integrationTestCompile(group: 'org.yaml', name: 'snakeyaml', version: '1.23') {
        force = true
    }

    functionalTestCompile(group: 'org.yaml', name: 'snakeyaml', version: '1.23') {
        force = true
    }

    integrationTestCompile sourceSets.main.runtimeClasspath
    integrationTestCompile sourceSets.test.runtimeClasspath

    functionalTestCompile sourceSets.main.runtimeClasspath
    functionalTestCompile sourceSets.test.runtimeClasspath

    smokeTestCompile sourceSets.main.runtimeClasspath
    smokeTestCompile sourceSets.test.runtimeClasspath
}

dependencyCheck {
    suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

gradle.startParameter.continueOnFailure = true

bootJar {
    archiveName = jarName
    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues.
// this setting only applies when running via gradle bootRun -- see web.config for the
// java flag that configures the deployed applications
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]
