buildscript {
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.4.34")
        classpath("org.flywaydb:flyway-database-postgresql:10.16.0")
    }
}

plugins {
    id 'application'
    id 'jacoco'
    id 'pmd'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id "info.solidsoft.pitest" version '1.15.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.sonarqube' version '5.1.0.4882'
    id 'org.springframework.boot' version '2.7.18'
    id "org.flywaydb.flyway" version '10.16.0'
    id "io.freefair.lombok" version "5.3.3.3"
    id 'uk.gov.hmcts.java' version '0.12.63'
    id 'au.com.dius.pact' version '4.1.7'
    id 'org.owasp.dependencycheck' version '10.0.3'
}

apply plugin: 'java'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'project-report'
apply plugin: 'idea'

def versions = [
        lombok             : '1.18.34',
        pitest             : '1.16.1',
        reformLogging      : '6.1.5',
        serenity           : '2.0.76',
        springBoot         : '2.7.18',
        springfoxSwagger   : '2.10.5',
        restAssured        : '4.3.3',
        jackson            : '2.17.2',
        launchDarklySdk    : '5.10.9',
        pact_version       : '4.1.7',
        log4j              : '2.23.1',
        springVersion      : '5.3.36',
        logback            : '1.2.13',
        bytebuddy          : '1.14.18',
        testContainer_postgresql: '1.20.0',
        bouncycastle       : '1.78.1',
        jwtVersion         : '0.12.6'
]

mainClassName = 'uk.gov.hmcts.reform.judicialapi.JudicialApplication'
def jarName = 'rd-judicial-api.jar'

group = 'uk.gov.hmcts.reform.juddata'
version = '0.0.1'

tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

sourceSets {

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }

    functionalTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

    smokeTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }

    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }

    pactTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/pactTest/java')
        }
        resources.srcDir file('src/pactTest/resources')
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.resources.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.resources.srcDirs
    }
}

project.ext {
    pactVersion = getCheckedOutGitCommitHash()
}

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

task contract(type: Test) {
    description = 'Runs the consumer Pact tests'
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    include "uk/gov/hmcts/reform/judicialapi/**"
}


task runProviderPactVerification(type: Test) {
    useJUnitPlatform()
    logger.lifecycle("Runs pact Tests")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    if (project.hasProperty('pact.verifier.publishResults')) {
        systemProperty 'pact.verifier.publishResults', project.property('pact.verifier.publishResults')
    }
    systemProperty 'pact.provider.version', project.pactVersion
    include "uk/gov/hmcts/reform/judicialapi/provider/**"
}

runProviderPactVerification.dependsOn contract

runProviderPactVerification.finalizedBy pactVerify

pact {
    publish {
        pactDirectory = 'pacts'
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
        tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
        version = project.pactVersion
    }
}

configurations {
    integrationTestImplementation.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    functionalTestImplementation.extendsFrom testCompile
    functionalTestRuntime.extendsFrom testRuntime
}

pmd {
    toolVersion = '6.55.0'
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest, sourceSets.functionalTest, sourceSets.smokeTest]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSets = [
            'category/java/errorprone.xml',
            'category/java/bestpractices.xml'
    ]
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    finalizedBy aggregate
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {

    systemProperty "scenario", System.getProperty('scenario')

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    finalizedBy aggregate
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
    baselineOnMigrate = true
    baselineVersion = '1.1'
    schemas = ['dbjudicialdata']
    locations = ["filesystem:src/main/resources/db/migration"]
    if (project.hasProperty("dburl")) {
        url = "jdbc:postgresql://${dburl}"
    }
    placeholders = ['dbReaderUserName' : System.getenv("DB_READER_USERNAME")]

}

task fortifyScan(type: JavaExec)  {
  mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
  // This is a temporary fix to prevent the nightly build from failing if the Fortify scan detects issues
  ignoreExitValue = true
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    executionData(test)
    reports {
        xml.required = true
        csv.required = false
        xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

pitest {
    junit5PluginVersion = '1.0.0'
    targetClasses = ['uk.gov.hmcts.reform.judicialapi.*']
    testSourceSets = [sourceSets.test]
    excludedClasses = [
            'uk.gov.hmcts.reform.judicialapi.configuration.*',
            'uk.gov.hmcts.reform.judicialapi.controller.*',
            'uk.gov.hmcts.reform.judicialapi.JudicialApplication',
            'uk.gov.hmcts.reform.judicialapi.elinks.configuration.ElinksFeignInterceptorConfiguration',
            'uk.gov.hmcts.reform.judicialapi.elinks.controller.advice.ForbiddenException',
            'uk.gov.hmcts.reform.judicialapi.elinks.service.impl.ELinksServiceImpl',
            'uk.gov.hmcts.reform.judicialapi.elinks.service.impl.ElinksPeopleServiceImpl',
            'uk.gov.hmcts.reform.judicialapi.elinks.service.impl.IdamElasticSearchServiceImpl',
            'uk.gov.hmcts.reform.judicialapi.elinks.configuration.SchedulerConfig',
            'uk.gov.hmcts.reform.judicialapi.elinks.configuration.SendGridConfig',
            'uk.gov.hmcts.reform.judicialapi.elinks.util.DataloadSchedulerJobAudit',
            'uk.gov.hmcts.reform.judicialapi.elinks.scheduler.ElinksApiJobScheduler'
    ]
    threads = 10
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 70
    useClasspathFile = true
}


project.tasks['pitest'].group = "Verification"

sonarqube {
    properties {
        property "sonar.projectName", "RD :: Judicial Reference api"
        property "sonar.projectKey", "RDJUDICIALAPI"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
        property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/judicialapi/configuration/**," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/JudicialApplication.java,"+
                "src/main/java/uk/gov/hmcts/reform/judicialapi/domain/**," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/controller/request/**," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/controller/TestingSupportController.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/controller/feign/IdamUserFeignClient.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/controller/response/**," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/service/impl/IdamUserProfileServiceImpl.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/repository/IdamUserProfileRepository.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/configuration/**," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/domain/**," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/response/**," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/configuration/ElinksFeignInterceptorConfiguration.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/controller/advice/ForbiddenException.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/service/impl/ELinksServiceImpl.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/service/impl/ElinksPeopleServiceImpl.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/service/impl/IdamElasticSearchServiceImpl.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/configuration/SchedulerConfig.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/configuration/SendGridConfig.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/util/DataloadSchedulerJobAudit.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/exception/**," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/scheduler/ElinksApiJobScheduler.java," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/service/impl/**," +
                "src/main/java/uk/gov/hmcts/reform/judicialapi/elinks/util/**,"
    }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {

    implementation 'org.testng:testng:7.10.2'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'org.seleniumhq.selenium', name: 'selenium-api', version: '3.141.59'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: versions.springBoot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: versions.springBoot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: versions.springBoot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
    implementation group: 'org.springframework.security', name: 'spring-security-core', version: '5.7.11'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache', version: versions.springBoot

    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: versions.bouncycastle

    implementation group: 'com.vladmihalcea', name: 'hibernate-types-52', version: '1.2.0'


    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.8'


    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache', version: versions.springBoot

    implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.8'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.17.2', ext: 'pom'

    implementation group: 'javax.inject', name: 'javax.inject', version: '1'
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation 'com.github.hmcts:idam-java-client:2.1.1'

    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: versions.log4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j', version: versions.log4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: versions.log4j
    implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '3.0.2'

    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.8.0'
    implementation 'com.sendgrid:sendgrid-java:4.10.2'
    implementation group: 'com.azure', name: 'azure-core', version: '1.50.0'
    implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: '7.17.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.11.0'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-freemarker', version: versions.springBoot

    implementation group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger


    implementation 'com.github.hmcts:service-auth-provider-java-client:4.1.2'

    implementation group: 'org.flywaydb', name: 'flyway-core', version: '10.16.0'
    implementation group: 'org.flywaydb', name: 'flyway-database-postgresql', version: '10.16.0'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.3'
    implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: versions.launchDarklySdk
    implementation group: 'commons-lang', name: 'commons-lang', version: '2.6'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.15.0'
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: versions.jwtVersion
    implementation group: 'io.jsonwebtoken', name: 'jjwt-impl', version: versions.jwtVersion
    implementation group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: versions.jwtVersion
    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.40'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: versions.logback
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback
    implementation group: 'org.springframework', name: 'spring-core', version: versions.springVersion
   	implementation group: 'org.springframework', name: 'spring-beans', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-aop', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-aspects', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-context', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-context-support', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-expression', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-jcl', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-jdbc', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-orm', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-tx', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-web', version: versions.springVersion
    implementation group: 'org.springframework', name: 'spring-webmvc', version: versions.springVersion

    implementation "com.github.hmcts.java-logging:logging:${versions.reformLogging}"
    implementation "com.github.hmcts.java-logging:logging-appinsights:5.1.9"
    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.4'
    implementation group: 'com.google.guava', name: 'guava', version: '32.1.3-jre'
    //Fix for CVE-2021-29425
    implementation 'commons-io:commons-io:2.16.1'

    //Added below dependencies for shedlock
    implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '5.14.0'
    implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: '5.14.0'

    implementation group: 'io.github.openfeign.form', name: 'feign-form', version: '3.8.0'
    implementation group: 'io.github.openfeign.form', name: 'feign-form-spring', version: '3.8.0'
    implementation "io.github.openfeign:feign-httpclient:11.10"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j
    implementation "com.github.hmcts.java-logging:logging:${versions.reformLogging}"
    implementation "com.github.hmcts.java-logging:logging-appinsights:${versions.reformLogging}"

    implementation group: 'com.nimbusds', name: 'lang-tag', version: '1.7'
    implementation group: 'org.json', name: 'json', version: '20230227'


    testImplementation ('com.github.hmcts:rd-commons-lib:0.0.18'){
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-web'
    }


    testImplementation("org.hamcrest:hamcrest-junit:2.0.0.0") {
        exclude group: "org.hamcrest", module: "hamcrest-core"
        exclude group: "org.hamcrest", module: "hamcrest-library"
    }

    testImplementation group: 'org.testcontainers', name: 'postgresql', version: versions.testContainer_postgresql
    testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testContainer_postgresql
    testImplementation group: 'org.testcontainers', name: 'testcontainers', version: versions.testContainer_postgresql

    testImplementation ("org.mockito:mockito-inline:4.11.0") {
        exclude group: "net.bytebuddy", module: "byte-buddy"
        exclude group: "net.bytebuddy", module: "byte-buddy-agent"
    }
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: versions.bytebuddy
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: versions.bytebuddy

    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.11.0'
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'

    testImplementation('com.github.tomakehurst:wiremock:2.35.2') {
        exclude group: 'com.github.jknack'
    }

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot


    testImplementation group: 'com.github.mifmif', name: 'generex', version: '1.0.2'

    testImplementation 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5', {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
    testImplementation 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.15.0'
    testImplementation group: 'org.pitest', name: 'pitest-junit5-plugin', version: '0.16'

    implementation(group: 'io.rest-assured', name: 'rest-assured', version: '3.3.0') {
        exclude group: "com.sun.xml.bind", module: "jaxb-osgi"
        exclude group: "org.apache.sling"
    }
    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.5.1'
    testImplementation group: 'io.rest-assured', name: 'rest-assured-common', version: '4.5.1'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot

    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.33'

    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version

    contractTestImplementation("org.junit.jupiter:junit-jupiter-api:5.10.3")
    contractTestImplementation("org.junit.jupiter:junit-jupiter-engine:5.10.3")
    contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pact_version
    contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pact_version
    contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pact_version

    integrationTestImplementation sourceSets.main.runtimeClasspath
    integrationTestImplementation sourceSets.test.runtimeClasspath

    functionalTestImplementation sourceSets.main.runtimeClasspath
    functionalTestImplementation sourceSets.test.runtimeClasspath

    smokeTestImplementation sourceSets.main.runtimeClasspath
    smokeTestImplementation sourceSets.test.runtimeClasspath

    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath

    pactTestImplementation sourceSets.main.runtimeClasspath
    pactTestImplementation sourceSets.test.runtimeClasspath
}

dependencyManagement {

    dependencies {
        // CVE-2021-42340
        // CVE-2023-28709
        dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.91') {
            entry 'tomcat-embed-core'
            entry 'tomcat-embed-el'
            entry 'tomcat-embed-websocket'
        }

        //CVE-2021-22044
        dependencySet(group: 'org.springframework.cloud', version: '3.1.9') {
            entry 'spring-cloud-starter-openfeign'
            entry 'spring-cloud-openfeign-core'
        }

        //CVE-2023-24998
        dependencySet(group: 'commons-fileupload', version: '1.5') {
            entry 'commons-fileupload'
        }
        //CVE-2022-40152
        dependencySet(group: 'com.fasterxml.woodstox', version: '6.7.0') {
            entry 'woodstox-core'
        }

        //        Resolves CVE-2023-4586
        dependencySet(group: 'io.netty', version: '4.1.112.Final') {
            entry 'netty-buffer'
            entry 'netty-codec'
            entry 'netty-codec-dns'
            entry 'netty-codec-http'
            entry 'netty-codec-http2'
            entry 'netty-codec-socks'
            entry 'netty-common'
            entry 'netty-handler'
            entry 'netty-handler-proxy'
            entry 'netty-resolver'
            entry 'netty-resolver-dns'
            entry 'netty-resolver-dns-classes-macos'
            entry 'netty-resolver-dns-native-macos'
            entry 'netty-transport'
            entry 'netty-transport-classes-epoll'
            entry 'netty-transport-classes-kqueue'
            entry 'netty-transport-native-epoll'
            entry 'netty-transport-native-kqueue'
            entry 'netty-transport-native-unix-common'
        }
    }
}

dependencyCheck {
    suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

gradle.startParameter.continueOnFailure = true

bootJar {
    archiveFileName = jarName
    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'org.bouncycastle'
                && (details.requested.name == 'bcpkix-jdk18on'
                    || details.requested.name == 'bcprov-jdk18on')
        ){
            details.useVersion versions.bouncycastle
        }
    }

    resolutionStrategy.dependencySubstitution {
        substitute module('com.google.inject:guice') using module('com.google.inject:guice:5.1.0') withoutClassifier()
    }
}

dependencies {
    modules {
        module("org.bouncycastle:bcpkix-jdk15on") {
            replacedBy("org.bouncycastle:bcpkix-jdk18on")
        }
        module("org.bouncycastle:bcprov-jdk15on") {
            replacedBy("org.bouncycastle:bcprov-jdk18on")
        }
    }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues.
// this setting only applies when running via gradle bootRun -- see web.config for the
// java flag that configures the deployed applications
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

rootProject.tasks.named("processSmokeTestResources") {
    duplicatesStrategy = 'include'
}

rootProject.tasks.named("processFunctionalTestResources") {
    duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
    duplicatesStrategy = 'include'
}

rootProject.tasks.named("processContractTestResources") {
    duplicatesStrategy = 'include'
}

